! - Axis 1 Y1(LOWER) Axis X Home program
! - User Unit : mm
! - Modifiers : ESY

GLOBAL INT HomeFlag(64) !nHomeDone(32)
GLOBAL REAL Save(64)(5)
INT TimeOut, HomingBuffer, AXIS, Direction, Repeatability_Patch
REAL Search_Vel, Commut_current, Home_Offset

!*******************************************************************	
! PLC user variable initialize START


! AXIS 	= 0				! axis number	
! PLC user variable initialize END

!*****************************************************************************
! User Define Variable
AXIS = 1
HomingBuffer = 1
Direction = -1	! Right = 1, Left = -1

nHomeDone(AXIS)	= 0

Search_Vel = 40
Home_Offset = -15
Repeatability_Patch = 0
!*****************************************************************************

!*****************************************************************************
! Define to default
TimeOut = 1000
Commut_current = XRMS(AXIS) * 0.6
HomeFlag(AXIS) = 0
!*****************************************************************************

!*****************************************************************************
! Set parameters for motor flags
DISP "Buffer %.0f: %.0f axis home start", HomingBuffer, AXIS
DISABLE (AXIS)
TILL ^MST(AXIS).#ENABLED
WAIT 100

SAFETYGROUP(AXIS)
Save(AXIS)(0) = VEL(AXIS)
Save(AXIS)(1) = ACC(AXIS)
Save(AXIS)(2) = DEC(AXIS)
Save(AXIS)(3) = KDEC(AXIS)
Save(AXIS)(4) = JERK(AXIS)

MFLAGS(AXIS).#DEFCON = 1
MFLAGS(AXIS).#OPEN = 0
FCLEAR; WAIT 100
!*****************************************************************************
CERRI(AXIS) = 1
CERRA(AXIS) = 1
CERRV(AXIS) = 1
!*****************************************************************************
!Set Dynamic brake Off
VELBRK(AXIS) = XVEL(AXIS)
MFLAGS(AXIS).#DBRAKE = 0
!*****************************************************************************

!*****************************************************************************
! Set Motion parameters for homing
VEL(AXIS) = Search_Vel
ACC(AXIS) = 1500
DEC(AXIS) = ACC(AXIS)
JERK(AXIS) = ACC(AXIS)*15
KDEC(AXIS) = JERK(AXIS)
!*****************************************************************************

!*****************************************************************************
! Disable the default response of the hardware and software limits
FMASK(AXIS).#CPE = 1
FDEF(AXIS).#CPE = 1
FMASK(AXIS).#RL = 1
FDEF(AXIS).#RL = 1
FMASK(AXIS).#LL = 1
FDEF(AXIS).#LL = 0
FDEF(AXIS).#SRL = 0
FDEF(AXIS).#SLL = 0
WAIT 10
!*****************************************************************************

!*****************************************************************************
! Commutation for each motor

!*****************************************************************************
MFLAGS(AXIS).#OPEN=1
WAIT 10
!*****************************************************************************
! Start homing - look for limit switch
DISP "Buffer %.0f: %.0f axis home start, move to search limit", HomingBuffer, AXIS
ENABLE (AXIS)
WAIT 5000
TILL MST(AXIS).#ENABLED

MFLAGS(AXIS).#OPEN=0
WAIT 10

IF Direction = 1
	JOG (AXIS), +
	TILL FAULT(AXIS).#RL, (TimeOut * 120)
	IF ^FAULT(AXIS).#RL
		DISP "Buffer %.0f: %.0f axis search limit fault", HomingBuffer, AXIS
		GOTO Time_Out
	END
ELSEIF Direction = -1
	JOG (AXIS), -
	TILL FAULT(AXIS).#LL, (TimeOut * 120)
	IF ^FAULT(AXIS).#LL
		DISP "Buffer %.0f: %.0f axis search limit fault", HomingBuffer, AXIS
		GOTO Time_Out
	END
END
HALT (AXIS)
TILL MST(AXIS).#INPOS
WAIT 500

DISP "Buffer %.0f: %.0f axis search limit complete, move to search index", HomingBuffer, AXIS
IST(AXIS).#IND = 1
WAIT 10
IST(AXIS).#IND = 0
WAIT 10
JOG/V (AXIS),(-Direction*Search_Vel/2)
TILL IST(AXIS).#IND,(TimeOut * 40)
IF ^IST(AXIS).#IND
	DISP "Buffer %.0f: %.0f axis search index fault", HomingBuffer, AXIS
	GOTO Time_Out
END
HALT (AXIS)
TILL MST(AXIS).#INPOS
WAIT 500
IF Repeatability_Patch
	PTP/E (AXIS), IND(AXIS) + POW(2,(E_SCMUL(AXIS)-3))*EFAC(AXIS)
	TILL MST(AXIS).#INPOS
	WAIT 500
	SET FPOS(AXIS) = FPOS(AXIS) - IND(AXIS) - GETCONF(265, AXIS)
ELSE
	SET FPOS(AXIS) = FPOS(AXIS) - IND(AXIS)
END
DISP "Buffer %.0f: %.0f axis search index complete", HomingBuffer, AXIS
!*****************************************************************************

!*****************************************************************************
! Move to zero position
PTP/E AXIS, 0
TILL MST(AXIS).#INPOS
WAIT 500
!*****************************************************************************

!*****************************************************************************
! Move to offset
PTP/E AXIS, Home_Offset
TILL MST(AXIS).#INPOS
WAIT 1000
SET FPOS(AXIS) = FPOS(AXIS) - FPOS(AXIS)
!*****************************************************************************

!*****************************************************************************
! Set software limits and unmask left/right limit faults
FMASK(AXIS).#CPE = 1
FDEF(AXIS).#CPE = 1
FMASK(AXIS).#RL = 1
FDEF(AXIS).#RL = 1
FMASK(AXIS).#LL = 1
FDEF(AXIS).#LL = 1
FDEF(AXIS).#SRL = 0
FDEF(AXIS).#SLL = 0
CERRI(AXIS)=1
!*****************************************************************************

!*****************************************************************************
! Set home done flags and encoder filter
HomeFlag(AXIS) = 1
DISP "Buffer %.0f: %.0f axis home complete", HomingBuffer, AXIS
!*****************************************************************************

!*****************************************************************************
! Set home done flags and encoder filter
!CALL INPUTSHAPING
!WAIT 1000
nHomeDone(AXIS) = 1
DISP "Buffer %.0f: %.0f axis home complete", HomingBuffer, AXIS
!*****************************************************************************
!*****************************************************************************
! Restore previous motion parameters
Restore:
VEL(AXIS)=Save(AXIS)(0)
ACC(AXIS)=Save(AXIS)(1)
DEC(AXIS)=Save(AXIS)(2)
KDEC(AXIS)=Save(AXIS)(3)
JERK(AXIS) = Save(AXIS)(4)
!*****************************************************************************
STOP

!*****************************************************************************
! Time out
Time_Out:
DISP "Buffer %.0f: %.0f axis home fault, Time_Out", HomingBuffer, AXIS
HALT (AXIS)
TILL MST(AXIS).#INPOS, TimeOut
DISABLE (AXIS)
GOTO Restore
STOP
!*****************************************************************************

!*****************************************************************************
! Set software limits and unmask left/right limit faults
ON ^PST(1).#RUN
	AXIS = 1
	FMASK(AXIS).#CPE = 1
	FDEF(AXIS).#CPE = 1
	FMASK(AXIS).#RL = 1
	FMASK(AXIS).#LL = 1
	FDEF(AXIS).#RL = 1
	FDEF(AXIS).#LL = 1
	FDEF(AXIS).#SRL = 0
	FDEF(AXIS).#SLL = 0
DISP "Buffer 1: 1 axis safety enable all"
RET
!*****************************************************************************
